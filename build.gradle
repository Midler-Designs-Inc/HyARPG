plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.0'
}

group = 'com.example.hyarpg'
version = '1.0.0'

repositories {
    mavenCentral()
    // Official Hytale Maven repository
    maven {
        name = 'hytale-release'
        url = 'https://maven.hytale.com/release'
    }
    maven {
        name = 'hytale-pre-release'
        url = 'https://maven.hytale.com/pre-release'
    }
}

dependencies {
    // Hytale Server API from official Maven repository
    compileOnly 'com.hypixel.hytale:Server:2026.02.19-1a311a592'
    // JSR305 annotations (@Nonnull, @Nullable)
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation(files("server/HyUI-0.8.8-2026-02-18.jar"))
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

shadowJar {
    archiveClassifier.set('')
    // Exclude server classes from the final JAR
    dependencies {
        exclude(dependency { it.moduleGroup == 'com.hypixel' })
    }
}

// Disable the default jar task to avoid conflicts with shadowJar
tasks.named('jar') {
    enabled = false
    finalizedBy("deployToServer")
}

tasks.named('build') {
    dependsOn shadowJar
}

// Deploy plugin JAR to server mods folder
tasks.register('deployToServer', Copy) {
    // Using 'from shadowJar' automatically adds task dependency and proper input tracking
    from shadowJar
    into 'C:\\Users\\jeffm\\AppData\\Roaming\\Hytale\\UserData\\Mods'
    doLast {
        println "Deployed to C:\\Users\\jeffm\\AppData\\Roaming\\Hytale\\UserData\\Mods"
    }
}

// Watch for changes and auto-rebuild (useful during development)
tasks.register('watch') {
    doLast {
        println "Watching for changes... Press Ctrl+C to stop."
        println "Run 'gradle build --continuous' for auto-rebuild on file changes."
    }
}